plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.1'
	id 'io.spring.dependency-management' version '1.1.5'
	id 'com.google.protobuf' version '0.9.4'
}

group = 'com.dy'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	grpcVersion = '1.51.1'
	protobufVersion = '3.25.3'
}

dependencies {
	// Spring 기본 구성
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	implementation 'org.springframework.kafka:spring-kafka'

	// gRPC 관련 라이브러리 (버전을 변수로 통일)
	implementation 'net.devh:grpc-spring-boot-starter:2.15.0.RELEASE'
	implementation 'javax.annotation:javax.annotation-api:1.3.2'
	// FCM SDK
	implementation 'com.google.firebase:firebase-admin:9.2.0'

	// 개발 및 테스트
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.mysql:mysql-connector-j'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:${protobufVersion}"
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
		}
	}
	generateProtoTasks {
		all().each { task ->
			// java 클래스 생성 설정
			task.builtins {
				java {}
			}
			// grpc 서비스 클래스 생성 설정
			task.plugins {
				grpc {}
			}
		}
	}
}

sourceSets {
	main {
		proto {
			// proto 파일이 위치한 실제 경로를 지정
			srcDir 'src/main/grpc'
		}
		java {
			// gRPC가 생성하는 자바 소스 경로 추가
			srcDirs 'build/generated/source/proto/main/java',
					'build/generated/source/proto/main/grpc'
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
}
