plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.1'
	id 'io.spring.dependency-management' version '1.1.5'
	id 'com.google.protobuf' version '0.9.4'
}

group = 'com.dy'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	grpcVersion = '1.58.0'
}

dependencies {
	// Spring 기본 구성
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	implementation 'org.springframework.kafka:spring-kafka'

	// gRPC 관련 라이브러리 (서버와 클라이언트 모두 추가)
	implementation 'net.devh:grpc-server-spring-boot-starter:3.1.0.RELEASE'
	implementation 'net.devh:grpc-client-spring-boot-starter:3.1.0.RELEASE'

	// FCM SDK
	implementation 'com.google.firebase:firebase-admin:9.2.0'

	// 개발 및 테스트
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.mysql:mysql-connector-j'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:3.25.3"
	}
	plugins {
		grpc {
			// 위에서 정의한 grpcVersion 변수를 사용하여 정확한 버전을 지정
			artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
		}
	}
	generateProtoTasks {
		all().each { task ->
			task.builtins {
				java {}
			}
			task.plugins {
				grpc {}
			}
		}
	}
}

sourceSets {
	main {
		proto {
			srcDir 'src/main/grpc'
		}
		java {
			srcDirs 'build/generated/source/proto/main/java',
					'build/generated/source/proto/main/grpc'
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
}